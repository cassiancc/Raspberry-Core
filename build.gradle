plugins {
    id 'dev.architectury.loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
    id 'org.moddedmc.wiki.toolkit' version '0.2.7'
    id "me.modmuss50.mod-publish-plugin" version "1.1.0"
    id 'co.uzzu.dotenv.gradle' version "4.0.0"
}

group = project.maven_group
version = project.mod_version + "+" + minecraft_version

base {
    archivesName = project.archives_name
}

loom {
    forge {
        mixinConfig 'raspberry.mixins.json'
    }
    mixin {
        afterEvaluate {
            defaultRefmapName = "raspberry.refmap.json"
        }
    }
}

wiki {
    // The name of the object (examplemod) should match the registered wiki project ID (if it exists).
    docs.create("raspberry-core") {
        // The path to the folder containing the documentation metadata file (sinytra-wiki.json)
        root = file("docs/")
    }
}

repositories {
    // Add repositories to retrieve artifacts from in here.
    // You should only use this when depending on other mods because
    // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
    // See https://docs.gradle.org/current/userguide/declaring_repositories.html
    // for more information about repositories.
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
    // Cloth Config
    exclusiveContent {
        forRepository {
            maven {
                name = "shedaniel"
                url = 'https://maven.shedaniel.me/'
            }
        }
        filter {
            includeGroup("me.shedaniel.cloth")
        }
    }
    // Blueprint
    exclusiveContent {
        forRepository {
            maven {
                name = "Team Abnormals"
                url = 'https://maven.teamabnormals.com/'
            }
        }
        filter {
            includeGroup("com.teamabnormals")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                url "https://cursemaven.com"
            }
        }
        filter {
            includeGroup "curse.maven"
        }
    }
    // Pollen
    exclusiveContent {
        forRepository {
            maven {
                name = "BlameJared"
                url = "https://maven.blamejared.com/"
            }
        }
        filter {
            includeGroup("gg.moonflower")
            includeGroup("gg.moonflower.pinwheel")
        }
    }
    // Create
    exclusiveContent {
        forRepository {
            maven {
                name = "Create"
                url = 'https://modmaven.dev/'
            }
        }
        filter {
            includeGroup("com.simibubi.create")
            includeGroup("com.tterrag.registrate")
            includeGroup("com.jozufozu.flywheel")
        }
    }
    // GeckoLib
    exclusiveContent {
        forRepository {
            maven {
                name = 'GeckoLib'
                url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
            }
        }
        filter {
            includeGroup("software.bernie.geckolib")
            includeGroup("com.eliotlash.mclib")
        }
    }
    // Parchment Mappings
    exclusiveContent {
        forRepository {
            maven {
                name = 'ParchmentMC'
                url = 'https://maven.parchmentmc.org'
            }
        }
        filter {
            includeGroup("org.parchmentmc.data")
        }
    }
    // Player Animator
    exclusiveContent {
        forRepository {
            maven {
                name = "KosmX's maven"
                url = 'https://maven.kosmx.dev/'
            }
        }
        filter {
            includeGroup("dev.kosmx.player-anim")
        }
    }
    // PalladiumCore
    exclusiveContent {
        forRepository {
            maven {
                name = "ThreeTag"
                url 'https://repo.repsy.io/mvn/lucraft/threetag'
            }
        }
        filter {
            includeGroup("net.threetag")
        }
    }
    // KubeJS
    exclusiveContent {
        forRepository {
            maven {
                name = "KubeJS"
                url = "https://maven.latvian.dev/releases"
            }
        }
        filter {
            includeGroup "dev.latvian.mods"
            includeGroup "dev.latvian.apps"
        }
    }
    // JitPack
    exclusiveContent {
        forRepository {
            maven {
                name = "JitPack"
                url = 'https://jitpack.io'
            }
        }
        filter {
            includeGroup "com.github.rtyley"
        }
    }
    // Local dependencies
    flatDir {
        dirs 'libs'
    }
    // EMI
    exclusiveContent {
        forRepository {
            maven {
                name = "TerraformersMC"
                url = "https://maven.terraformersmc.com/"
            }
        }
        filter {
            includeGroup "dev.emi"

        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "MixinSquared"
                url = "https://maven.bawnorton.com/releases"
            }
        }
        filter {
            includeGroup("com.github.bawnorton.mixinsquared")
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "Illusive Soulworks maven"
                url = "https://maven.theillusivec4.top/"
            }
        }
        filter {
            includeGroup("top.theillusivec4.curios")
        }
    }
}

dependencies {
    minecraft "net.minecraft:minecraft:$project.minecraft_version"
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-1.20.1:2023.09.03@zip")
    }
    forge "net.minecraftforge:forge:$project.forge_version"

    // Cloth Config
    modApi("me.shedaniel.cloth:cloth-config-forge:$cloth_config_version")

    // Abnormals
    modCompileOnly("maven.modrinth:environmental:$environmental_version")
    modCompileOnly("maven.modrinth:caverns-and-chasms:$caverns_and_chasms_version")
    modCompileOnly("maven.modrinth:upgrade-aquatic:$upgrade_aquatic_version")
    modCompileOnly("maven.modrinth:neapolitan:$neapolitan_version")
    modImplementation("com.teamabnormals:blueprint:$blueprint_version")
    modCompileOnly("maven.modrinth:endergetic:$endergetic_version")
    modImplementation("maven.modrinth:buzzier-bees:$buzzier_bees_version")
    modImplementation("maven.modrinth:autumnity:$autumnity_version")

    // Recipe viewers
    modImplementation("dev.emi:emi-forge:${emi_version}+$minecraft_version")
    modImplementation("maven.modrinth:jei:$jei_version-forge")

    // Farmer's Delight and its addons
    modImplementation("maven.modrinth:farmers-delight:$farmers_delight_version")
    modCompileOnly("curse.maven:brewin-and-chewin-637808:$brewin_and_chewin_version")
    modCompileOnly("maven.modrinth:cultural-delights:$cultural_delights_version")
    modCompileOnly("maven.modrinth:my-nethers-delight:$nethers_delight_version")

    // Create, its dependencies, and its dependents
    modCompileOnly("com.simibubi.create:create-$minecraft_version:$create_forge_version:slim") { transitive = false }
    modCompileOnly("com.tterrag.registrate:Registrate:$registrate_forge_version")
    modCompileOnly("com.jozufozu.flywheel:flywheel-forge-$minecraft_version:$flywheel_forge_version")

    // Optional dependencies for mods with only mixin fixes
    modImplementation("curse.maven:aquaculture-60028:$aquaculture_version")
    modImplementation("curse.maven:copper-tuff-backport-fabric-950738:$copper_backport_version")
//    modImplementation("curse.maven:copperized-567666:$copperized_version")
    modImplementation("curse.maven:connectible-chains-forge-418514:$connectible_chains_version")
    modImplementation("software.bernie.geckolib:geckolib-forge-$geckolib_version")
    implementation("com.eliotlash.mclib:mclib:20")
    forgeRuntimeLibrary("com.eliotlash.mclib:mclib:20")
    modImplementation("maven.modrinth:supplementaries:$supplementaries_version-forge")
    modImplementation("maven.modrinth:moonlight:$moonlight_lib_version-forge")
    modImplementation("maven.modrinth:naturalist:$naturalist_version")
    modImplementation("maven.modrinth:midnightlib:$midnightlib_version")
//    modImplementation("maven.modrinth:another-furniture:$another_furniture_version")
    modImplementation("maven.modrinth:oreganized:$oreganized_version")
    modImplementation("maven.modrinth:spelunkery:$spelunkery_version-forge")
    modImplementation("maven.modrinth:sullysmod:$sullys_version")
    modImplementation("maven.modrinth:twigs:$twigs_version")
    modCompileOnly("curse.maven:dungeons-mobs-428883:$dungeons_mobs_version")
    modCompileOnly("curse.maven:dungeons-libraries-550941:$dungeons_libraries_version") // dependency of dungeons mobs
    modImplementation("curse.maven:enderman-overhaul-574409:$enderman_overhaul_version")
    modImplementation("curse.maven:resourceful-config-714059:$resourceful_config_version") // dependency of enderman overhaul
    modImplementation("curse.maven:resourceful-lib-570073:$resourceful_lib_version") // dependency of enderman overhaul
    modImplementation("maven.modrinth:quark:$quark_version")
    modImplementation("maven.modrinth:zeta:$zeta_version")
//    modImplementation("maven.modrinth:dynamiccrosshair:$dynamic_crosshair_version-forge")
    modImplementation("maven.modrinth:cofh-core:$cofh_core_version-forge")
    modImplementation("curse.maven:clash-468706:$clash_version")
    modCompileOnly("maven.modrinth:gliders:$gliders_version")
//    modRuntimeOnly("dev.kosmx.player-anim:player-animation-lib-forge:${player_anim_version}")
//    modRuntimeOnly("net.threetag:PalladiumCore-forge:${palladiumcore_version}")
    modCompileOnly("maven.modrinth:survivality:${survivality_version}")
    modCompileOnly("local:ecologics-forge:1.19.2-2.1.11") // jar built locally to avoid errors when importing the project
    modImplementation("maven.modrinth:screenshot-viewer:${screenshot_viewer_version}")
    modImplementation("maven.modrinth:spyglass-improvements:${spyglass_improvements_version}")
    modRuntimeOnly("top.theillusivec4.curios:curios-forge:${curios_version}") // dependency of spyglass improvements
    modImplementation("maven.modrinth:toms-storage:$toms_simple_storage_version")
    modCompileOnly("curse.maven:controllable-317269:4445137")
    modImplementation("maven.modrinth:architects-palette:${architects_palette_version}")
    modCompileOnly("maven.modrinth:cooks-collection:$cooks_collection_version")
    modCompileOnly("maven.modrinth:jade:$jade_version")
    modImplementation("maven.modrinth:xaeros-minimap:${xaeros_minimap_version}")
    modImplementation("maven.modrinth:xaeros-world-map:${xaeros_worldmap_version}")
    modImplementation("curse.maven:etched-491890:${etched_version}")

    // mods for testing purposes
    modImplementation("curse.maven:map-atlases-forge-519759:6301932")
    modImplementation("maven.modrinth:mmmmmmmmmmmm:zAahRq2q")

    // beacons
    modImplementation("curse.maven:just-enough-beacons-reforged-912266:$jeb_reforged_version")
    modImplementation("curse.maven:cerbons-better-beacons-921066:$better_beacons_version")
    modImplementation("curse.maven:cerbons-api-955605:$cerbons_api_version")

    modImplementation("curse.maven:mining-master-534956:5540513")
    //1.20
    modImplementation "maven.modrinth:better-copper-armor-and-tools:1.2-1.20.1"
    modImplementation "dev.architectury:architectury-forge:9.2.14"
    modImplementation("maven.modrinth:puzzles-lib:${puzzles_lib_version}")
    modImplementation("maven.modrinth:echo-chest:${echo_chest_version}")
    modImplementation("maven.modrinth:feature-recycler:${feature_recycler_version}")
    modImplementation("maven.modrinth:item-obliterator:${item_obliterator_version}")
    modImplementation("maven.modrinth:necronomicon:${necronomicon_version}")

    // MixinSquared
    compileOnly(annotationProcessor("com.github.bawnorton.mixinsquared:mixinsquared-common:0.2.0"))
    implementation(include("com.github.bawnorton.mixinsquared:mixinsquared-forge:0.2.0"))
    // MixinExtras
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:0.4.1"))
    implementation(include("io.github.llamalad7:mixinextras-forge:0.4.1"))

}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand version: project.version
    }
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

// Configure Maven publishing.
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = base.archivesName.get()
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}


publishMods {
    file = remapJar.archiveFile
    type = BETA
    modLoaders.add("forge")
    changelog = file('CHANGELOG-LATEST.md').text.trim()

    modrinth {
        projectId = "1dE2nxJB"
        accessToken = env.MODRINTH_API_KEY.orNull()
        minecraftVersions.add(minecraft_version)
        requires("blueprint")
        requires("supplementaries")
        requires("farmers-delight")
    }

    curseforge {
        projectId = "1dE2nxJB"
        accessToken = env.CURSEFORGE_API_KEY.orNull()
        minecraftVersions.add(minecraft_version)
        requires("blueprint")
        requires("supplementaries")
        requires("farmers-delight")
    }
}